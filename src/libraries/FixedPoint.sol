// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.26;

import "./BitMath.sol";
import "@openzeppelin/contracts/utils/math/Math.sol";

library FixedPoint {
    // range: [0, 2**112 - 1]
    // resolution: 1 / 2**112
    struct uq112x112 {
        uint224 _x;
    }

    uint8 public constant RESOLUTION = 112;
    uint256 public constant Q112 = 0x10000000000000000000000000000; // 2**112

    error Overflow();

    error DivisionZero();

    // returns a UQ112x112 which represents the ratio of the numerator to the denominator
    // can be lossy
    function fraction(uint256 numerator, uint256 denominator) internal pure returns (uq112x112 memory) {
        require(denominator > 0, DivisionZero());
        if (numerator == 0) return FixedPoint.uq112x112(0);

        if (numerator <= type(uint144).max) {
            uint256 result = (numerator << RESOLUTION) / denominator;
            require(result <= type(uint224).max, Overflow());
            return uq112x112(uint224(result));
        } else {
            uint256 result = Math.mulDiv(numerator, Q112, denominator);
            require(result <= type(uint224).max, Overflow());
            return uq112x112(uint224(result));
        }
    }
}